;
; Function to make adding flexible axles to a MOC easier.
;
; Hazen 02/15
;

(import locate :local)

; Position / orientation helper function.
(def ftr (po fn)
  (translate ((aref po 0) (aref po 1) (aref po 2)) ; Translate to position.
   (rotate ((aref po 3) (aref po 4) (aref po 5))   ; Rotate to curve orientation.
    (fn))))

; Create an axle of the specified length that goes along the specified curve.
; The curve starts after the initial straight end stud.
;
; length - The total length of the axle in bricks / studs.
; a-curve - The curve function that the axle should follow.
; color - The color of the axle.
; stretch - Amount to stretch sub-parts to minimize the appearance of breaks in a bent axle.
;
(def flexible-axle (length a-curve color :stretch 1.05)
 (block

  (def disp 2)

  (def start-parts (list "s\faxle1.dat" 
			 "s\faxle2.dat" 
			 "s\faxle3.dat" 
			 "s\faxle4.dat" 
			 "s\faxle5.dat"))

  ; hmm, maybe a reverse function would be useful..
  (def end-parts (list "s\faxle5.dat" 
		       "s\faxle4.dat" 
		       "s\faxle3.dat" 
		       "s\faxle2.dat" 
		       "s\faxle1.dat"))

  ; stud start element.
  (ftr (a-curve 0) 
       (lambda ()
  	(tr 0 0 0 90 0 0 
  	    (lambda () 
  	     (scale (1 5 1) (part "stud3a.dat" color))))))

  ; start parts
  (for (p start-parts)
   (ftr (a-curve disp)
	(lambda ()
	 ; translate so that the part is rotated around it's center
	 ; instead of around it's end.
	 (tr 0 0 2 0 -90 0
	     (lambda ()
	      (scale (stretch 1 1) (part p color))))))
   (set disp (+ disp 4)))

  ; middle parts
  (for (p (* 5 (- length 4)))
   (ftr (a-curve disp)
	(lambda ()
	 (tr 0 0 2 -90 0 0
	     (lambda ()
	      (scale (1 (* 4 stretch) 1) (part "axlehol8.dat" color))))))
   (set disp (+ disp 4)))

  ; end parts
  (for (p end-parts)
   (ftr (a-curve disp)
	(lambda ()
	 ; translate so that the part is rotated around it's center
	 ; instead of around it's end.
	 (tr 0 0 -2 0 90 0
	     (lambda ()
	      (scale (stretch 1 1) (part p color))))))
   (set disp (+ disp 4)))

  ; stud end element.
  (set disp (- disp 2))
  (ftr (a-curve disp) 
       (lambda ()
  	(tr 0 0 0 -90 0 0 
  	    (lambda () 
  	     (scale (1 5 1) (part "stud3a.dat" color))))))
  ))

