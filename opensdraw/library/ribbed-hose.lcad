;
; Function to make adding ribbed hose to a MOC easier.
;
; Hazen 03/15
;

; Specify the full path to the shapes module, otherwise Python will not be able to find it
; as this will likely not be executed in the current working directory.
(pyimport opensdraw.library.shapes)

;
; Create a ribbed hose with specified number of segments that goes along a curve. The hose
; is created using primitives, it should be in a group so that you can specify the color
; and back face culling. This is also faster if you need multiple identical hoses.
;
; Note: For reasons unclear to me, since they both appear to have exactly the same part
;       spacing (6.2) in their .dat files, the segments in this hose will not be exactly 
;       in register with those in the LDraw part 71944, and this apart is 0.5 LDU longer 
;       over a distance of 19 segments.
;
; Example:
;
;  (group "my-hose.ldr"
;   (header "!LDRAW_ORG Unofficial_part")
;   (header "BFC CERTIFY CCW")
;   (ribbed-hose 20 my-curve))
;  (part "my-hose.ldr" 10)
;
; segments - The number of ribs in the ribbed hose.
; a-curve - The curve function that the ribbed hose should follow.
;
(def ribbed-hose (segments a-curve)
 (block

  (create-sub-parts)

  (def dist 0)
  (def d-inc 3.1)

  ; Start segment.
  (transform (a-curve dist)
   (part "ribbed-hose-end" 16))
  (set dist (+ dist 2.5))

  (transform (a-curve dist)
   (part "ribbed-hose-inner" 16)
   (part "ribbed-hose-outer" 16))
  (ribbed-hose-ring a-curve (- dist 2.5) (- dist 1.0) dist t)
  (ribbed-hose-ring a-curve (- dist 2.5) (- dist 1.5) dist nil)
  (ribbed-hose-ring a-curve (+ dist d-inc) (+ dist 1.0) dist nil)
  (ribbed-hose-ring a-curve (+ dist d-inc) (+ dist 1.5) dist t)

  ; Middle segments.
  (for (i (- segments 1))
   (set dist (+ dist d-inc))
   (transform  (a-curve dist)
    (part "ribbed-hose-cylinder" 16))

   (set dist (+ dist d-inc))
   (ribbed-hose-ring a-curve (- dist d-inc) (- dist 1.0) dist t)
   (ribbed-hose-ring a-curve (- dist d-inc) (- dist 1.5) dist nil)
   (ribbed-hose-ring a-curve (+ dist d-inc) (+ dist 1.0) dist nil)
   (ribbed-hose-ring a-curve (+ dist d-inc) (+ dist 1.5) dist t)

   (transform (a-curve dist)
    (part "ribbed-hose-inner" 16)
    (part "ribbed-hose-outer" 16)))

  ; End piece.
  (set dist (+ dist 2.5))
  (transform (a-curve dist)
   (rotate (list 0 180 0)
    (part "ribbed-hose-end" 16)))

  ))

; Draw the rings that connect the sub-parts.
(def ribbed-hose-ring (a-curve inner center outer ccw)
 (block

  ; Inner vector.
  (def in-v (vector 6.5  0 (- center inner)))

  ; Outer vector.
  (def out-v (vector 8.01 0 (- center outer)))

  ; Use shapes module to draw the ring.
  (ring (a-curve inner) in-v (a-curve outer) out-v ccw)

  ))

;; 
;; Sub-parts that the ribbed hose is assembled from.
;;
(def ribbed-hose-cylinder ()
 (rotate (list 90 0 0)
  (header "~OpenLDraw Technic Ribbed Hose Cylinder")
  (header "Based on 80.dat by William Howard [WilliamH]")
  (header "!LICENSE Redistributable under CCAL version 2.0 : see CAreadme.txt")
  (header "!LDRAW_ORG Unofficial_part")

  (comment "BFC CERTIFY CCW")

  ; The rib is 3 long, so this part needs to be 3.2 long.
  (transform (list 0 2.1 0 0.5 0 0 0 -1 0 0 0 -0.5)
   (part "4-4Rin13" 16))

  (transform (list 0 -2.1 0 0.5 0 0 0 1 0 0 0 -0.5)
   (part "4-4Rin13" 16))

  (transform (list 0 1.6 0 6.5 0 0 0 6.5 0 0 0 -6.5)
   (part "t01i0769" 16))

  (transform (list 0 -1.6 0 6.5 0 0 0 -6.5 0 0 0 -6.5)
   (part "t01i0769" 16))

  (comment "BFC INVERTNEXT")
  (transform (list 0 -1.6 0 6 0 0 0 3.2 0 0 0 6)
   (part "4-4cyli" 16))

  (transform (list 0 -1.6 0 6.5 0 0 0 3.2 0 0 0 6.5)
   (part "4-4cyli" 16))

  (transform (list 0 1.6 0 6.5 0 0 0 1 0 0 0 6.5)
   (part "4-4edge" 16))

  (transform (list 0 -1.6 0 6.5 0 0 0 1 0 0 0 6.5)
   (part "4-4edge" 16))

  ))

(def ribbed-hose-end ()
 (rotate (list 90 0 0)
  (header "~OpenLDraw Technic Ribbed Hose End")
  (header "Based on 80.dat by William Howard [WilliamH]")
  (header "!LICENSE Redistributable under CCAL version 2.0 : see CAreadme.txt")
  (header "!LDRAW_ORG Unofficial_part")

  (comment "BFC CERTIFY CCW")

  (transform (list 0 0 0 6 0 0 0 1 0 0 0 6)
   (part "4-4edge" 16))

  (transform (list 0 0 0 6.5 0 0 0 1 0 0 0 6.5)
   (part "4-4edge" 16))

  (comment "BFC INVERTNEXT")
  (transform (list 0 0 0 0.5 0 0 0 -1 0 0 0 0.5)
   (part "4-4rin12" 16))

  (comment "BFC INVERTNEXT")
  (transform (list 0 0 0 6 0 0 0 1 0 0 0 6)
   (part "4-4cyli" 16))

  (transform (list 0 0 0 6.5 0 0 0 1 0 0 0 6.5)
   (part "4-4cyli" 16))

  (transform (list 0 1 0 6.5 0 0 0 6.5 0 0 0 -6.5)
   (part "t01i0769" 16))
  ))

(def ribbed-hose-inner ()
 (rotate (list 90 0 0)
  (header "~OpenLDraw Technic Ribbed Hose Inner")
  (header "Based on s\78s01.dat by William Howard [WilliamH]")
  (header "!LICENSE Redistributable under CCAL version 2.0 : see CAreadme.txt")
  (header "!LDRAW_ORG Unofficial_part")

  (comment "BFC CERTIFY CCW")

  (comment "BFC INVERTNEXT")
  (transform (list 0 -0.5 0 8.5 0 0 0 1 0 0 0 8.5)
   (part "4-4cyli" 16))

  (comment "BFC INVERTNEXT")  
  (transform (list 0 -0.5 0 0 0 8 0 -8 0 -8 0 0)
   (part "t01o0625" 16))

  (comment "BFC INVERTNEXT")  
  (transform (list 0 0.5 0 0 0 8 0 8 0 -8 0 0)
   (part "t01o0625" 16))
  ))

(def ribbed-hose-outer ()
 (rotate (list 90 0 0)
  (header "~OpenLDraw Technic Ribbed Hose Outer")
  (header "Based on s\78s01.dat by William Howard [WilliamH]")
  (header "!LICENSE Redistributable under CCAL version 2.0 : see CAreadme.txt")
  (header "!LDRAW_ORG Unofficial_part")

  (comment "BFC CERTIFY CCW")

  (transform (list 0 0 0 9 0 0 0 -1 0 0 0 9)
   (part "4-4cyli" 16))

  (transform (list 0 0 0 9 0 0 0 1 0 0 0 9)
   (part "4-4cyli" 16))

  (transform (list 0 -1 0 8 0 0 0 -4 0 0 0 8)
   (part "t04o1250" 16))

  (transform (list 0 1 0 8 0 0 0 4 0 0 0 8)
   (part "t04o1250" 16))

  (transform (list 0 -1 0 0 0 -8 0 -4 0 8 0 0)
   (part "t04o1250" 16))

  (transform (list 0 1 0 0 0 -8 0 4 0 8 0 0)
   (part "t04o1250" 16))

  (transform (list 0 -1 0 -8 0 0 0 -4 0 0 0 -8)
   (part "t04o1250" 16))

  (transform (list 0 1 0 -8 0 0 0 4 0 0 0 -8)
   (part "t04o1250" 16))

  (transform (list 0 -1 0 0 0 8 0 -4 0 -8 0 0)
   (part "t04o1250" 16))

  (transform (list 0 1 0 0 0 8 0 4 0 -8 0 0)
   (part "t04o1250" 16))

  ))

; Create sub-part groups, only once.
(def initialized nil)
(def create-sub-parts ()
 (if (not initialized)
   (block
    (set initialized t)
    (group "ribbed-hose-cylinder.dat"
     (ribbed-hose-cylinder))
    (group "ribbed-hose-end.dat"
     (ribbed-hose-end))
    (group "ribbed-hose-inner.dat"
     (ribbed-hose-inner))
    (group "ribbed-hose-outer.dat"
     (ribbed-hose-outer))
    )))


