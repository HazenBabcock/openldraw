;
; Function to make adding flexible axles to a MOC easier.
;
; Hazen 02/15
;

(import curve-utils :local)
(import locate :local)

;
; Create an axle of the specified length that goes along the specified curve.
; The curve starts after the initial straight end stud.
;
; length - The total length of the axle in bricks / studs.
; a-curve - The curve function that the axle should follow.
; color - The color of the axle.
; stretch - Amount to stretch sub-parts to minimize the appearance of breaks in a bent axle.
;
(def flexible-axle (length a-curve color :stretch 1.05)
 (block

  (def disp 2)

  (def start-parts (list "s\faxle1" 
			 "s\faxle2" 
			 "s\faxle3" 
			 "s\faxle4" 
			 "s\faxle5"))

  ; hmm, maybe a reverse function would be useful..
  (def end-parts (list "s\faxle5" 
		       "s\faxle4" 
		       "s\faxle3" 
		       "s\faxle2" 
		       "s\faxle1"))

  ; stud start element.
  (ftr (a-curve 0) 
       (lambda ()
  	(trf 0 0 0 90 0 0 
	     (lambda () 
	      (scale (vector 1 5 1) (part "stud3a" color))))))

  ; start parts
  (for (p start-parts)
   (ftr (a-curve disp)
	(lambda ()
	 ; translate so that the part is rotated around it's center
	 ; instead of around it's end.
	 (trf 0 0 (* 2 stretch) 0 -90 0
	      (lambda ()
	       (scale (vector stretch 1 1) (part p color))))))
   (set disp (+ disp 4)))

  ; middle parts
  (for (p (* 5 (- length 4)))
   (ftr (a-curve disp)
	(lambda ()
	 (trf 0 0 (* 2 stretch) -90 0 0
	      (lambda ()
	       (scale (vector 1 (* 4 stretch) 1) (part "axlehol8" color))))))
   (set disp (+ disp 4)))

  ; end parts
  (for (p end-parts)
   (ftr (a-curve disp)
	(lambda ()
	 ; translate so that the part is rotated around it's center
	 ; instead of around it's end.
	 (trf 0 0 (* -2 stretch) 0 90 0
	      (lambda ()
	       (scale (vector stretch 1 1) (part p color))))))
   (set disp (+ disp 4)))

  ; stud end element.
  (set disp (- disp 2))
  (ftr (a-curve disp) 
       (lambda ()
  	(trf 0 0 0 -90 0 0 
	     (lambda () 
	      (scale (vector 1 5 1) (part "stud3a" color))))))
  ))
